:\ ^key 10 = unless \ ;
:( ^key 41 = unless ( ;
~( The two lines above defines comments. We do not have them till there )

~( boolean constant )
|true   ^1 ;
|false  ^0 ;

|on     ^true ^;
|off    ^false ^;
:on  ^on ;
:off ^off ;

:eon! ^on echo! ;
:eoff! ^off echo! ;
:% ^eoff! ;

~( helper words )
|2R> ^R> R> ;
|2>R ^>R >R ;

|2dup ^dup dup ;
|2drop ^drop drop ;

|1+ ^1 + ;
|1- ^1 - ;
:1+ ^1+ ;
:1- ^1- ;

:+! ^dup @ rot + swap ! ;
:1+! ^dup @ 1+ swap ! ;
:1-! ^dup @ 1- swap ! ;
:cell+! ^dup @ cell + swap ! ;

|;; ^R> drop ;

|cells ^cell * ;
:cells ^cells ;

:noname> ^here @ ;

~( loop )
:times-loop ~( xt n - ) ^1- over execute dup when times-loop ;
:times ~( ent n - ) ^swap code> swap
:ctimes ~( xt n - ) ^times-loop 2drop ;
~'times-loop ~hide-entry

~( another loop form )
:-done?; ^1 - dup when ;  drop ;; ;

~( rendering )
:cr ^10 emit ;
:tab ^9 emit ;
:space ^32 emit ;

~( dump word )
:emit.; ^ drop 46 emit ;; ;
:v-emit ~( c - ) ^
  dup  32 < when emit.;
  dup 128 < unless emit.;
  emit ;

:dumpc ^swap dup c@ dup v-emit space . tab  1+ swap ;
:dumpl ^dumpc -done?; dumpl ;
:dumpb ^swap  12 dumpl  swap cr -done?; dumpb ;
:dump ~( a - ) ^10 dumpb drop ;
~'dumpc ~hide-entry  'dumpl ~hide-entry  'dumpb ~hide-entry


:alloc ~( n - a ) ^here @ swap over + here ! ;

:>! ^cell alloc ! ;
:>c! ^1 alloc ! ;

~10000 alloc :dataheap ~>_ ^;
~cell alloc :dhere ~>_ ^;
~dataheap dhere !

:dalloc ~( n - a ) ^dhere @ swap over + dhere ! ;

:>d! ^cell dalloc ! ;
:>dc! ^1 dalloc ! ;

:? ^@ . ;
:variable ^cell dalloc dup >_ ! ;
:variables ^cells dalloc >_ ;

|[] ^cells + ;
:[] ^[] ;

~( create null-terminated string )
:"init ^dhere @ cell + ;
:"loop ^dup key dup ~key " >_ ^= when ; swap c! 1 + "loop ;
:"done ^drop drop dhere @ - dup ^1 + dalloc swap cell - over ! ;
:" ^"init "loop "done ;
:c" ^" >_ ;


~( print string )
:."init ^dup dup @ + cell + swap cell + ;
:."loop ^over over = when ; dup c@ emit 1 + ."loop ;
:pr
:." ^."init ."loop 2drop ;
:prn ^pr cr ;
:nprn ^cr pr cr ;


~( system )
:cmd ^cell + system ;

~( marker/forget )
:marker ^latest @  i-latest @   here @ 8 cells +   >_ >_ >_ ;
:forget ^latest !  i-latest !   here ! ;

~( math )
:drop; ^drop ;; ;
:min ^over over < when drop; swap drop ;
:max ^over over < unless drop; swap drop ;
:> ^swap < ;

~( files )
:aload ~( filename - buf ) ^
   dup file-size cell + c-alloc
   dup rot load ;

~( mem move )
:-cmove-done?; ^1- dup when ;  2drop drop ;; ;
:cmove-advance ^ 1+ ;
:cmove-loop ^
   >R  swap dup c@ >R cmove-advance
   swap dup R> swap c! cmove-advance
   R>  -cmove-done?; cmove-loop
;
:cmove ~( src dst n - ) ^
   `cmove-advance `1+ ^is
   cmove-loop ;

:cmove> ~( src dst n - ) ^
  1- >R R@ + swap R@ + swap R> 1+
  `cmove-advance `1- ^is
  cmove-loop ;

~( eval )
:ptr ~0 variable ^;
:count ~0 variable ^;
:loop ^ptr @ c@ parse  ptr 1+!  count 1-!  count @ unless ; loop ;
:eval ~( string - ) ^clear-tib  dup cell + ptr !  @ count !  loop  32 parse ;
'ptr ~hide-entry  'count ~hide-entry  'loop ~hide-entry
