%
~\ usage: cat lib.cf | ./colorforth examples/colorize.cf

:[esc0 ^27 emit ~key [ [,] ^emit ~key 0 [,] ,;
:[esc1 ^[esc0 emit ~key 1 [,] ^emit ~key ; [,] ^emit ,;
:[esc  ^[esc1 ~key 9 [,] ^emit ,;
:esc]  ^emit ~key m [,] ^emit ,;

:eof? ^-1 = ,;

:clear!    ^[esc0 esc] ,;
:red!     ^[esc ~key 1 [,] ^esc] ,;
:green!   ^[esc ~key 2 [,] ^esc] ,;
:yellow!  ^[esc ~key 3 [,] ^esc] ,;
:blue!    ^[esc ~key 4 [,] ^esc] ,;
:magenta! ^[esc ~key 5 [,] ^esc] ,;
:cyan!    ^[esc ~key 6 [,] ^esc] ,;
:white!   ^[esc1 ~key 3 [,] ^emit ~key 7 [,] ^esc] ,;

:dump-loop ^getchar dup emit over = ~-if ^dump-loop ~then ^drop ,;
:dump-to-char ^swap emit dump-loop yellow! ,;

:<stop>
:stop ^drop ;;; ,;
:with-prefix ^`nop `<stop> ^is ,;
:without-prefix ^`stop `<stop> ^is ,;

:.char
   ^dup eof? ~if ^clear! bye ~then
   ^dup ~key : [,] ^= ~if ^red! <stop> ~then
   ^dup ~key ^ [,] ^= ~if ^green! <stop> ~then
   ^dup ~key ~ [,] ^= ~if ^yellow! <stop> ~then
   ^dup ~key ' [,] ^= ~if ^blue! <stop> ~then
   ^dup ~key ` [,] ^= ~if ^blue! <stop> ~then
   ^dup ~key , [,] ^= ~if ^cyan! <stop> ~then
   ^dup ~key ( [,] ^= ~if ^white! ~then

   ^dup ~key \ [,] ^= ~if ^10 white! dump-to-char ,; ~then
   ^dup ~key " [,] ^= ~if key " [,] ^magenta! dump-to-char ,; ~then

   ^emit ,;

:colorize ^getchar .char colorize ,;

~without-prefix colorize
